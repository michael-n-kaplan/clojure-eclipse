import types;
 
process(xtext::GeneratedMetamodel this) :
	process(ePackage)
;
 
process(ecore::EPackage this):
	eClassifiers.typeSelect(ecore::EClass).process();
 
process(ecore::EClass this):
	switch (name) {
		case "NameBinding" : processNameBinding(this) 
		case "Lambda": removeStructuralFeature(this, "bindings")
		case "LexicalScope": addChildReference(this, "bindings", ePackage.getEClassifier("Binding"))
		case "Let": removeStructuralFeature(this, "bindings")
		case "Loop": removeStructuralFeature(this, "bindings")
		case "SymbolDef": addSuperType(this, org::eclipse::xtext::common::types::TypesPackage::eINSTANCE.getEClassifier("JvmType"))
		default:	null
	};
	
processNameBinding(ecore::EClass this):
	addSuperType(this, ePackage.getEClassifier("SymbolDef"))->
	removeStructuralFeature(this, "name")->
	removeStructuralFeature(this, "meta");
	
addSuperType(ecore::EClass this, ecore::EClassifier c):
	this.eSuperTypes.add(c)->
	this;

addChildReference(ecore::EClass this, String name, ecore::EClassifier type):
	if this.getEStructuralFeature(name) == null then
		this.eStructuralFeatures.add(newChildReference(name, type))->
		this;
	
removeStructuralFeature(ecore::EClass this, String ref):
	this.eStructuralFeatures.remove(this.getEStructuralFeature(ref))->
	this;
	
newChildReference(String name, ecore::EClassifier type):
	let r = new ecore::EReference :
		r.setName(name)->
		r.setEType(type)->
		r.setContainment(true)->
		r.setUpperBound(-1)->
		r.setLowerBound(0)->
		r;