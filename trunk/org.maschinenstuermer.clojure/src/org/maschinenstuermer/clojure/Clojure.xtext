grammar org.maschinenstuermer.clojure.Clojure 
hidden (WSS,SL_COMMENT)

generate clojure "http://www.maschinenstuermer.org/clojure/Clojure"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

File:
	forms+=Form*;

SimpleForm:
	SimpleLiteral|
	SpecialForm|
	Seq;
		
Form:
	SimpleForm|
	Literal;
		
QuotedForm:
	QuotedLiteral|
	QuotedSeq;

QuotedSeq:
	QuotedList|
	QuotedVector|
	QuotedMap|
	QuotedSet;
	
QuotedList:
	{ List }
	'(' elements+=QuotedForm* ')';

QuotedVector:
	{ Vector }
	'[' elements+=QuotedForm* ']';
		
QuotedMap:
	{ Map }
	'{' (keys+=QuotedForm elements+=QuotedForm)* '}';
	
QuotedSet:
	{ Set }
	'#{' elements+=QuotedForm* '}';

QuotedLiteral:
	SimpleLiteral|
	symbol=(QualifiedName|'&');
	
SimpleLiteral:
	value='nil'|
	value=STRING|
	value=NUMBER|
	value=KEYWORD|
	value=Boolean;
	
Literal:
	symbol=[types::JvmType|QualifiedName];

Boolean:
	"true"|"false";
	
SpecialForm:
	Call|
	Deref|
	Do|
	Dot|
	If|
	Let|
	Loop|
	Meta|
	MonitorEnter|
	MonitorExit|
	New|
	Ns|
	Quote|
	Recur|
	Regex|
	SymbolDef|
	SyntaxQuote|
	Throw|
	Try|
	Unquote|
	UnquoteSplicing|
	Var;

SymbolDef:
	Def|
	Defn|
	Defmacro|
	Fn;
	 
// Special forms

Def:
	'(' 'def' meta=ReaderMeta? name=Name init=Form? ')';
	
Defn:
	'(' 'defn' meta=ReaderMeta? name=Name doc=STRING? attributes=Map? body=Body ')';

Defmacro:
	'(' 'defmacro' meta=ReaderMeta? name=Name doc=STRING? attributes=Map? body=Body ')';
	 
Deref:
	'(' 'deref' form=Form ')'|
	'@' form=Form;
	
Do:
	{ Do }
	'(' 'do' exprs+=Form* ')';
	
Dot:
	'(' '.' args+=Form+ ')';
	
Fn:
	{ Fn }
	('(' ('fn'|'fn*') meta=ReaderMeta? name=Name? body=Body ')');
	
If:
	'(' 'if' test=Form then=Form else=Form? ')';
	
LexicalScope:
	Lambda|
	Let|
	Loop;
	
Let:
	{ Let }
	'(' 'let' '['(bindings+=Binding inits+=Form)*']' exprs+=Form* ')';
	
Loop:
	{ Loop }
	'(' 'loop' '['(bindings+=Binding inits+=Form)*']' exprs+=Form* ')';
	
Ns:
	'(' 'ns' meta=Meta? name=QualifiedName ')';
	
Meta:
	'(' 'meta' form=Form ')'|
	ReaderMeta;
	
ReaderMeta:
	'^' form=Form;

MonitorEnter:
	'(' 'monitor-enter' form=Form ')';
		
MonitorExit:
	'(' 'monitor-exit' form=Form ')';
		
New:
	'(' 'new' class=QualifiedName args+=Form*')';
	
Recur:
	{ Recur }
	'(' 'recur' exprs+=Form* ')';
	
Quote:
	'(' 'quote' form=QuotedForm ')'|
	'\'' form=QuotedForm;
		
Regex:
	'#' regex=STRING;
	
SyntaxQuote:
	'`' form=Form;
	
Throw:
	'(' 'throw' expr=Form ')';
	
Try:
	{ Try }
	'(' 'try' exprs+=Form* (catchClauses+=CatchClause)* finallyClause=FinallyClause? ')';
	
CatchClause:
	'(' 'catch' className=Name name=Name exprs+=Form* ')';
	
FinallyClause:
	{ FinallyClause }
	'(' 'finally' exprs+=Form* ')';
	
Unquote:
	'~' form=Form;
	
UnquoteSplicing:
	'~@' form=Form;
	
Var:
	('(' 'var' symbol=[SymbolDef|Name] ')') |
	('#\'' symbol=[SymbolDef|Name]);

// Bindings
	
Binding:
	SimpleBinding |
	VectorBinding |
	MapBinding;

NameBinding:
	meta=ReaderMeta? varArg?='&'? name=Name;
			
SimpleBinding:
	bindings+=NameBinding;
	
VectorBinding:
	{ VectorBinding }
	'[' bindings+=Binding* ']';
	
MapBinding:
	{ MapBinding }
	'{' bindings+=KeyBinding* '}';

KeyBinding:
	binding+=Binding key += KEYWORD; 

// fn 
	
Body:
	lambdas+=Lambda | 
	('(' lambdas+=Lambda ')')+;
	
Lambda:
	{ Lambda }
	'[' bindings+=Binding* ']' exprs+=Form*;

// Sequences
	
Seq:
	List|
	Vector|
	Map|
	Set;
	
Call:
	'(' fn=[types::JvmType|JavaQualifiedName] args+=Form* ')';
	
List:
	{ List }
	'(' (elements+=SimpleForm elements+=Form*)? ')';

Vector:
	{ Vector }
	'[' elements+=Form* ']';
		
Map:
	{ Map }
	'{' (keys+=Form elements+=Form)* '}';
	
Set:
	{ Set }
	'#{' elements+=Form* '}';

// data type rules
	
JavaQualifiedName:
	ID ('.' ID)* ('/' Name)?;
	
QualifiedName:
	Name ('.' Name)* ('/' Name)?;
	

Name:
	ID|
	'catch'|
	'def'|'defn'|'defmacro'|'deref'|'do'|
	'fn'|'fn*'|'finally'|
	'if'|
	'let'|'loop'|
	'meta'|'monitor-enter'|'monitor-exit'|
	'ns'|'new'|
	'quote'|
	'throw'|'try'|
	'recur'|
	'var';
	
// Terminals
	
terminal KEYWORD:
	':' ID;
	
terminal ID: 
	(SYMBOL_START (SYMBOL_START|'0'..'9')*)|
	('&' SYMBOL_START (SYMBOL_START|'0'..'9')*);
		
terminal SYMBOL_START:
	'a'..'z'|'A'..'Z'|'_'|'+'|'-'|'*'|'!'|'?'|'='|'#'|'<'|'>'|'/';
		
terminal NUMBER: 
	('0'..'9')+;
	
terminal STRING	: 
	'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"';
	
terminal WSS: 
	(' '|'\t'|'\r'|'\n')+;
	
terminal SL_COMMENT: 
	';' !('\n'|'\r')* ('\r'? '\n')?;
	